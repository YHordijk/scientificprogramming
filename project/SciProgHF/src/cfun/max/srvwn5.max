
 This routine of short-range-(LDA-VWN5)corelation contains two parts,
 the first is the well known SVWN5 correlation, and the second is the
 MU- dependent part from the reference:
 J. Toulouse, A. Savin and H-J Flad, IJQC  100, (6) 1047 (200).

PI: 3.14159265358979312;
rs(rhoa,rhob):= (3/(4*PI*(rhoa+rhob)))^(1/3);
x(rhoa,rhob):= sqrt(rs(rhoa,rhob));
zeta(rhoa,rhob):= (rhoa-rhob)/(rhoa+rhob);
f(rhoa,rhob):= ((1+zeta(rhoa,rhob))^(4/3) + (1-zeta(rhoa,rhob))^(4/3) -2)/(2^(4/3)-2);
 
/* Evaluate f''(0) */
d2f0: 4/(9*(2^(1/3) - 1));

/* Parameters [paramagnetic, ferromagnetic, spin stiffness] */

x0:[-0.10498,-0.32500,-0.00475840];
A: [0.0621814,0.0310907,-1/(3*PI^2)];
b: [3.72744,7.06042,1.13107];
c: [12.9352,18.0578,13.0045];

/* Paramagnetic contribution */
X_p(rhoa,rhob):= x(rhoa,rhob)*x(rhoa,rhob) + b[1]*x(rhoa,rhob) + c[1];
X0_p: x0[1]*x0[1] + b[1]*x0[1] + c[1]; /* X_p(x0) */
Q_p: sqrt(4*c[1] - b[1]*b[1]);

/* Write paramagnetic functional as sum/product of 6 terms: */
t1_p(rhoa,rhob):= log(x(rhoa,rhob)^2/X_p(rhoa,rhob));
t2_p(rhoa,rhob):= (2*b[1]/Q_p)*atan(Q_p/(2*x(rhoa,rhob) + b[1]));
t3_p: b[1]*x0[1]/X0_p;
t4_p(rhoa,rhob):=log((x(rhoa,rhob) - x0[1])^2/X_p(rhoa,rhob));
t5_p: 2*(b[1] + 2*x0[1])/Q_p;
t6_p(rhoa,rhob):=atan(Q_p/(2*x(rhoa,rhob) + b[1]));

E_para(rhoa,rhob):=  0.5 * A[1]*(t1_p(rhoa,rhob) + t2_p(rhoa,rhob) - t3_p*(t4_p(rhoa,rhob) + t5_p*t6_p(rhoa,rhob)));

/* dfine Mu-dependentpart (1+ C1 MU + C2  MU^2)  */

/* VWN5 parametrization of the lDA correlation energy */

L(rhoa,grada,rhob,gradb,gradab):=  (E_para(rhoa,rhob) )*(rhoa+rhob);


/* define parameters for he MU-dependent part */

u1:1.0271;
u2:-0.2302;
v1:0.6197;

C11(rhoa,rhob):=( u1 * rs(rhoa,rhob) + u2 * (rs(rhoa,rhob))^2 );
C12(rhoa,rhob):=( 1 + v1 * rs(rhoa,rhob) );
C1(rhoa,rhob):=( C11(rhoa,rhob)/C12(rhoa,rhob) );

DC:( 32/(3*PI) );
AC:3.2581;
bet:163.44;
gam:4.7125;
gamr(rhoa,rhob):= (gam + rs(rhoa,rhob) );

g0(rhoa,rhob):=  DC * ( (gamr(rhoa,rhob))^(3/2) + bet ) * exp(-AC *sqrt(gamr(rhoa,rhob)))  ; 

C2(rhoa,rhob):= 2 * E_para(rhoa,rhob) /( PI * (rhoa+rhob) *  (g0(rhoa,rhob)  -1/2) ); 
   
dnom(rhoa,rhob):= 1 + C1(rhoa,rhob) * MU + C2(rhoa,rhob) * MU^2;

K(rhoa,grada,rhob,gradb,gradab):= L(rhoa,grada,rhob,gradb,gradab) / dnom(rhoa,rhob);


